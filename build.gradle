// Top-level build file where you can add configuration options common to all
// sub-projects/modules.
buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}

apply plugin: 'com.android.application'

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'https://maven.google.com'
    }
    maven {
        url "https://jitpack.io"
    }
}

configurations {
    playstoreImplementation
    freeImplementation
}

ext {
    supportLibVersion = '27.0.2'
}

dependencies {
    implementation project(':libs:MemorizingTrustManager')
    playstoreImplementation 'com.google.android.gms:play-services-gcm:11.8.0'
    implementation 'org.sufficientlysecure:openpgp-api:10.0'
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'
    implementation "com.android.support:support-v13:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-emoji:$supportLibVersion"
    freeImplementation "com.android.support:support-emoji-bundled:$supportLibVersion"
    implementation 'org.bouncycastle:bcmail-jdk15on:1.56'
    implementation 'org.jitsi:org.otr4j:0.22'
    implementation 'org.gnu.inet:libidn:1.15'
    implementation 'com.google.zxing:core:3.2.1'
    implementation 'com.google.zxing:android-integration:3.2.1'
    implementation 'de.measite.minidns:minidns-hla:0.2.4'
    implementation 'de.timroes.android:EnhancedListView:0.3.4'
    implementation 'me.leolin:ShortcutBadger:1.1.19@aar'
    implementation 'com.kyleduo.switchbutton:library:1.2.8'
    implementation 'org.whispersystems:signal-protocol-java:2.6.2'
    implementation 'com.makeramen:roundedimageview:2.3.0'
    implementation "com.wefika:flowlayout:0.4.1"
    implementation 'net.ypresto.androidtranscoder:android-transcoder:0.2.0'
    implementation 'com.github.scottyab:showhidepasswordedittext:0.8'
}

ext {
    travisBuild = System.getenv("TRAVIS") == "true"
    preDexEnabled = System.getProperty("pre-dex", "true")
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 247
        versionName "1.23.3"
        archivesBaseName += "-$versionName"
        applicationId "eu.siacs.conversations"
    }

    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
        jumboMode true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions("distribution")

    productFlavors {
        playstore {
            dimension "distribution"
        }
        free {
            dimension "distribution"
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }



    if(new File("signing.properties").exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file("signing.properties")))

        signingConfigs {
            release {
                storeFile file(props['keystore'])
                storePassword props['keystore.password']
                keyAlias props['keystore.alias']
                keyPassword props['keystore.password']
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    }

    lintOptions {
        disable 'MissingTranslation', 'InvalidPackage', 'MissingQuantity', 'AppCompatResource'
    }

    subprojects {

        afterEvaluate {
            if (getPlugins().hasPlugin('android') ||
                    getPlugins().hasPlugin('android-library')) {

                configure(android.lintOptions) {
                    disable 'AndroidGradlePluginVersion', 'MissingTranslation'
                }
            }

        }
    }

    packagingOptions {
        exclude 'META-INF/BCKEY.DSA'
        exclude 'META-INF/BCKEY.SF'
    }
}
